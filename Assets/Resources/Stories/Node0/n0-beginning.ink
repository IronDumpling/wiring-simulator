INCLUDE ../0-global.ink

#title:？？？

->beginning

=== beginning ===

-随一阵颠簸，你周遭的声音忽然大了。你听见橡胶与石头摩擦的声音，前方传来一阵惊呼…… #speaker:爬虫脑
你在哪？你感受着自己的身体……它瘫软着，倚在某个摇摇晃晃的事物上。在你旁边，某种黏腻的液体正反复碰撞，发出“啵”样的声响。

// select_1

 * [很陌生。]->select_1_1
 * [是了——熟悉的海浪。]->select_1_2

=select_1_1

你还记得潮湿的触感，那是淅淅沥沥的雨水，清爽、流动。可你感受到了——几滴液体飞溅到你的脸上，它们是沉重的，仿佛要将你的面部拖曳下去……
鼻腔向你发出警告。那是股咸腥的气味。
->going_1

=select_1_2

你还记得海风的触感，潮湿、咸腥。在岸上走动时，那股气息总是刺激你的鼻腔，直至熟稔。
可一股独属于泥土的腥气混进来——它越来越近了。
你回忆中的沿岸逐渐模糊，直至变了颜色……成为一片红色泥泞，还有生长其上的茂密雨林。
->going_1

=going_1

——你在哪？你再次诘问自己。
*[（尝试睁开眼睛）]#speaker:
-你睁开——又忽地眯了眼。#speaker:
喑蓝色的深空中，太阳几乎缩成一个斑点，却比以往更加煞人。#title:海上
你感受到你的皮肤，它先被汗给浸湿，又被晒干了水分，只留下一片油腻，甚至析出盐来。
你慢慢起身。
*[（环顾四周）]
-这是艘蓝色的橡皮艇。船上有十来个人，大多是黑色皮肤。
你就坐在他们中间，听那些人用陌生的语言谈笑。白色的浪花在你身旁涌动，说话声与波涛此起彼伏，又被更为嘈杂的嗡鸣所遮掩——那是发动机的噪音。
忽然，你瞧见了旁边的中年男人。他有着和你一样的黄色皮肤，戴一顶卡其色的鸭舌帽。显然，他和你来自同一个国家。他抱着背包，两脚叉得很开，做着非常悠闲的姿势。
似乎是注意到你的目光，他轻轻挑了下眉。

VAR NPC1 = "偷渡客"
VAR know_about_OceanSleepTime = false
VAR know_about_NPC1comeform = false

->dialogue_1

=dialogue_1

-{怎么了？|还有问题吗？} #speaker:偷渡客
~temp question = 0
{know_about_OceanSleepTime:
~question ++
}
{know_about_NPC1comeform:
~question ++
}


//select_2
{

-question != 2: 

 *[我睡了多久？] #speaker:
 ~know_about_OceanSleepTime = true
 ->select_2_1
 
 *[你是从哪来的？] #speaker:
 ~know_about_NPC1comeform = true
 ->select_2_2
 
 -else:
 
 *[我们好像快到了。]#speaker: 
 **[继续]->going_2
 }
 
=select_2_1
 四个小时了，兄弟。#speaker:偷渡客
 哈，你心可真够大的。你至少该跟我打声招呼。
  *[为什么？]#speaker:
 -话音刚落，他朝你挪了挪身子，一边谨慎地环顾四周。#speaker:偷渡客
 没看到周围吗？他们全都是南美来的偷渡者。这群人手脚脏得很。
 你想想，这群人要去国外避难，除了讨口子，多半是做过脏活。
 他把身子靠得更近。#speaker:
 说难听点，有些就是尾随你来的，专门盯着你动手。#speaker:偷渡客
 
 *[你不能这么揣测别人。]#speaker: //select_2_1_1
 
男人笑出声。他在阳光里眯了眼。#speaker:
哈，兄弟，这只是半途，咱们还没到地方。前面还有好几个小国，连着一整个墨西哥。#speaker:偷渡客 //自由之路半途
别对他们抱太大同理心。我给你举个例子。
说着，男人小心地指了指前头。你循着看过去，见那里正并排坐着三个高大的男人，咧着嘴谈笑。#speaker:
左边那个杀过人，中间那个是强奸犯。#speaker:偷渡客
 
 **[你怎么知道？]#speaker:
他们刚才就在谈这事，甚至炫耀。#speaker:偷渡客
 
 ***[你听得懂他们说话？]#speaker:
来之前做了点功课，但是没用。给点钱就能请个翻译。#speaker:偷渡客
总之，这趟路比你想得凶险。

->dialogue_1_going_1


 *[感谢提醒，我会注意。]#speaker: //select_2_1_2
 
 嗯哼。你都到这儿了，最好别功亏一篑。#speaker:偷渡客
 我听说不少死半道上的，先是被骗到没钱，然后不得不铤而走险，比如租几条不结实的木头小船，最后——
 他意味深长地看了眼旁边的海面。#speaker:
 
 [智力检定] #check:INT+1d8>easy
 {CHECK:
 - SUCCESS:你还记得前几日发生的事：一队偷渡者在横渡海峡时遇上风暴，当晚就失踪了七人。
 - HUGE_SUCCESS:你还记得前几日发生的事：一队偷渡者在横渡海峡时遇上风暴，当晚就失踪了七人。
 - FAIL:这听起来更像威胁。
 - HUGE_FAIL:这听起来更像威胁。
 }
 
->dialogue_1_going_1

 
 =dialogue_1_going_1
 
 不瞒你说，你已经被偷过一次东西了。#speaker:偷渡客
 
-[感知检定] #speaker: #check:WIS+1d8>medium

 {CHECK:
 - SUCCESS:他似乎一直在等说这句话的机会。
 - HUGE_SUCCESS:他似乎一直在等说这句话的机会。
 - FAIL:这句话——连同内容都非常突然。
 - HUGE_FAIL:这句话——连同内容都非常突然。
 }
 
*{CHECK == SUCCESS || CHECK == HUGE_SUCCESS}[什么情况？]#speaker:
*{CHECK == FAIL || CHECK == HUGE_FAIL}[什么情况？！]#speaker:
* -> //INKY和unity有时候都会在运行check之前报错，这里是保险选项，也可以作为参考。

-就在你睡觉的时候，后面那个小个子想捞你的手机。#speaker:偷渡客
你正想回头，偷渡客忽然拍上你的肩，劝阻样地拽了一下。#speaker:
别急——他没偷着，我正好瞧见了，他没敢动手。#speaker:偷渡客
他说着，几乎是戏谑样地朝小偷笑笑。矮个子撇了撇嘴，咕哝出一句你听不懂的语言。男人笑得更大声了。#speaker:
瞧，别在这儿睡着了，不然就等着徒步穿行半个大洲吧。#speaker:偷渡客
他安静下来，又恢复了先前的姿势。#speaker:

 ->dialogue_1
 
 =select_2_2
 我？和你一样呗。还能是新加坡的不成？#speaker:偷渡客
 ->selector_2_2
 
 
 =selector_2_2
 {怎么，想和老乡聊聊天了？|接着问吧。|还想问什么吗？}#speaker:偷渡客
 {他大方地笑起来，似乎是来了兴致。|他说着，一边打量着你。|}#speaker:
 VAR know_about_NPC1way = false
 VAR know_about_NPC1name = false
 
 {-not know_about_NPC1name||not know_about_NPC1way:
 
//select_2_2
*[哪个省的？]#speaker:
这我可不能随便说。#speaker:偷渡客
他先是故作严肃，又一下子松了语气。#speaker:
好吧，至少是西南地区。我老家发展不好，自己出来也借了点钱，总归是勉强够了。#speaker:偷渡客

~ NPC1 = "莘先生"
~know_about_NPC1name = true

->selector_2_2

*[你走的哪条线？]
从土耳其到厄尔多瓜。沿途都有产业链，比想象中还方便。
前面走河道和陆路，到集结点以后，最后坐个车就能抵达哥斯达黎加。
这是最常用的路线了，稳妥一点。
~know_about_NPC1way = true

->selector_2_2

-else:
*[你为什么会走线呢？]
男人轻哼了一口气。#speaker:
这问题……比较隐私。当然，我也可以说。#speaker:偷渡客
无非“家道中落”，环境变差，最后迫于生计，只能出来找找活路，至少想喘口大气……#speaker:偷渡客
他忽然噤声了。似乎是意识到自己戾气太重，他咳嗽两声，又恢复到先前的姿势，敛了些笑。#speaker:
大概是这样。听说不少走线的和我一样，生活所迫。#speaker:偷渡客
那你呢？
->select_2_2_3
}

=select_2_2_3

*[生活过不下去。]#speaker:
哦，那很正常。大环境如此，上升空间也就那样了。#speaker:偷渡客
他自顾自地摸了摸下巴。#speaker:
狗急了还会跳墙。人要是被逼着走线，那估计过得是真够惨的。#speaker:偷渡客
就当我同情你吧，老兄。
//~HP = HP + 9
//~SAN = SAN + 3
//~MON = MON + 5

*[我想追求更好的生活。]#speaker:
有理想，伙计。不过谁也说不准到底能不能变得更好。#speaker:偷渡客
走线是几乎搭上性命的旅程，如果你做了准备，我也没太多好指摘的。
他眯眼看你一会儿。你看不清那是同情还是嘲讽。#speaker:
不过开弓没有回头箭——先专注于眼前吧。#speaker:偷渡客
//~HP = HP + 4
//~SAN = SAN + 6
//~MON = MON + 7

*[我被封杀了，只能出来混。]#speaker:
男人显然是愣住了。半晌，他拍了拍你的肩。
不容易啊，兄弟。你应该是我见过最惨的那种了。#speaker:偷渡客
那你更要到地方了。到时候摊牌，在镜头前说一通，那帮人也拿你没办法。我看好你。
//~HP = HP + 6
//~SAN = SAN + 6
//~MON = MON + 5

*[我想保密。]#speaker:
哦，哦！这可不公平！#speaker:偷渡客
他大声说着，但并没有生气的意思。#speaker:
好吧，开个玩笑，我完全理解。都出来了，谁还没有点难堪的过往呢。#speaker:偷渡客
事已至此，先专注于眼前吧。


-他朝你竖一个拇指。#speaker: 
[心情增加10]#Mood:+10

->dialogue_1


 
=going_2

 你瞧见远处的陆地逐渐清晰。莘先生也向着那边眺望。#speaker:
 是要上岸了，又要忙活一阵…… #speaker:偷渡客
 说着，他拉开背包外层，一只手伸进里边摸索。很快，男人手里多了两袋塑料封装的面包。他将其中一个递给你。
 
 *[感谢。] #speaker: 
 -[获得消耗品：面包] #object:+面包
 *[继续]

-随又一阵橡皮艇与石头摩擦的声音响起，小船靠上了海岸。你感觉身下一阵颠簸。船夫吆喝着偷渡者们下船。你和莘先生都起过身，拿上自己的背包。#title:隘口海岸
浅滩并不热闹，你只能远远望见零散的人影朝岸上走去。或许是海路过于凶险，鲜有人优先选择这条线路。
//但更前边——当地的居民已经准备好了。
男人又走到你身侧。那个黄色的包现在在他背上了。#speaker:
我问过蛇头，岸上没有休整的地方，我们得再走一段。#speaker:偷渡客

[感知检定] #speaker: #check:WIS+1d8>easy

 {CHECK:
 - SUCCESS:显然，他想邀请你同路。他还在试探你的意见。
 - HUGE_SUCCESS:显然，他想邀请你同路。他还在试探你的意见。
 - else:你等着他继续开口。
 }

还有，我跟了个队……明天就到阿坎迪雨林前的村子。#speaker:偷渡客
他抬眼盯着你，甚至带了些祈求的意思。#speaker:
你有打算吗？#speaker:偷渡客

-(going_2_1)

*[我准备单独走过去。]

你听见他吸了口气。#speaker:
朋友，隘口出了名的不安全，整个地盘都被当地组织掌控得死死的，经常有人被打劫、殴打，甚至*强奸*。#speaker:偷渡客
听我一劝，无论如何都要跟一个蛇头。“花钱消灾”不是白说的。

    **[你说的有道理。]#speaker:
    
    他抚了抚胸口，似乎终于放松下来。
    听得进就好。那现在怎么说？#speaker:偷渡客
    
    ->going_2_1
    
    **[我自有打算。]#speaker:
    
    他往后退了两步，显得焦躁不安。
    ……好吧，可能你有别的计划，我也不太清楚。#speaker:偷渡客
    但无论什么打算，记得安全至上，朋友。
    他说得很慢，一边拍拍你的肩。#speaker:
    我姓莘。上面一个草字头，下面一个辛苦的辛。你想怎么叫都行。#speaker:偷渡客
    祝你顺利，朋友。希望我们能在下个村子碰头。#speaker:莘先生
    他走开了，最后朝你挥了挥手。#speaker:
    
        ***[继续]->solo
        
    
*[我想找个蛇头。]

哦——当然。要找蛇头就去那边。#speaker:偷渡客
他朝一个木屋的方向指指，那边排着长队。#speaker:
那群人*参差不齐*，说不定还有专门拐卖的骗子，一定要注意甄别。#speaker:偷渡客
他说得很慢，一边拍拍你的肩。#speaker:
我姓莘。上面一个草字头，下面一个辛苦的辛。你想怎么叫都行。#speaker:偷渡客
祝你顺利，朋友。希望我们能在下个村子碰头。#speaker:莘先生
他走开了，最后朝你挥了挥手。#speaker:

    **[继续]->gang


*[我能跟你们的队伍吗？]

当然，没人不乐意。蛇头该倒欠我一笔中介费了。#speaker:偷渡客
他又笑起来——一如既往。#speaker:
你可以先和蛇头谈谈，他应该只会让你先交定金，到地方以后再补全款。我们一会儿就出发，行吧？#speaker:偷渡客

[感知检定] #speaker: #check:WIS+1d8>medium

 {CHECK:
 - SUCCESS:他说得飞快，似乎难掩高兴的情绪，话里多了种微妙的熟悉感。
 - HUGE_SUCCESS:他说得飞快，似乎难掩高兴的情绪，话里多了种微妙的熟悉感。
 - else:他说得飞快，似乎难掩高兴的情绪。
 }

[智力检定] #speaker: #check:INT+1d8>medium

 {CHECK:
 - SUCCESS:你不得不承认，他这时说起话来的确很像中介——如果真的是呢？
 - HUGE_SUCCESS:你不得不承认，他这时说起话来的确很像中介——如果真的是呢？
 - else:你不得不承认，或许群聚是人的本能——至少你被他的热情感染了。
 
 [心情增加10] #Mood:+10
 
 }
 
    **[说定了。]
    
    很好，那我先去等你。#speaker:偷渡客
    他指了个方向，一小挫人正围绕着一块石头，似乎正等着出发。#speaker:
    我姓莘。上面一个草字头，下面一个辛苦的辛。你想怎么叫都行。#speaker:偷渡客
    祝你顺利，朋友，队伍里见。#speaker:莘先生
    他走开了，最后朝你挥了挥手。#speaker:
    
        ***[继续]->team
    
    
    **[我还是想单独行动。]

    哦——当然，我理解。#speaker:偷渡客
    他看起来大失所望。#speaker:
    蛇头应该都在那边，你可以去问问有没有队伍。这种买卖没人嫌弃的。#speaker:偷渡客
    他朝一个木屋的方向指指，那边排着长队。#speaker:
    但是——那群人*参差不齐*，说不定还有专门拐卖的骗子，一定要注意甄别。小心当地的黑帮。#speaker:偷渡客
    他说得很慢，一边拍拍你的肩。#speaker:
    我姓莘。上面一个草字头，下面一个辛苦的辛。你想怎么叫都行。#speaker:偷渡客
    祝你顺利，朋友。希望我们能在下个村子碰头。#speaker:莘先生
    他走开了，最后朝你挥了挥手。#speaker:

        ***[继续]
        
        与莘先生作别，你临到了下一个选择。
        
        ****[（自行联系蛇头）]->gang
        ****[（独自进入雨林）]->solo


-(solo)

整备结束后，你独身进入了雨林。 #title:沿海雨林
你还记得莘先生对你的警告：这是一片由当地黑帮控制的地区。你早在新闻报道上听说过他们的大名——遇到他们就象征着旅途结束。没有蛇头领导，你只能凭借自己的判断避开他们。
形形色色的影子从周围晃过。你迎头撞上了一队偷渡者。
他们的领队对你很不欢迎，始终吆喝着你往旁边靠——或者立马入队，把钱给补上。

*[我不觉得这些道路是你们的专利。]

    话音刚落，领队显然听懂了你的意思。他用当地语言粗暴地冲你大喊，甚至走上前来。其他人都好奇地望着这边。
    
    **[（给他一拳）]
    
    ---(hit_point)
    
    [体质检定] #speaker: #check:STR+1d8>medium

    {CHECK:
    - SUCCESS:趁他还未反应，你抬手猛给了他一拳——你听见清脆的*咯啦*一声，想必是打上了颧骨。他向后退了一步，但并没有倒下，而是转头怒视着你。
    这是你出国以来第一次感觉畅快无比，仿佛疲惫与悔恨都清扫一空。
    ~ hit_point_1++
    [心情增加10] #Mood:+10 
    - HUGE_SUCCESS:趁他还未反应，你抬手猛给了他一拳——你听见清脆的*咯啦*一声，想必是打上了颧骨。他向后一个踉跄，差些摔倒在地上。他有些惊恐地看着你。
    这是你出国以来第一次感觉畅快无比，仿佛疲惫与悔恨都清扫一空。
    ~ hit_point_1++
    ~ hit_point_1++
    [心情增加10] #Mood:+10
    - FAIL:趁他还未反应，你抬手给了他一拳。你感受到拳锋传来一阵疼痛——而他仍站在原地，用脸颊顶着你的拳头，怒视着你。
    - else:趁他还未反应，你抬手给了他一拳——你本来是这样打算的。但你竟脚步一滑，差些把自己摔到地上，拳头在碰到对方脸上以前就歪了道。他用不可思议的眼神看着你。
    }
    
    下一刻，领队用嗓音怒吼了几声，也将把他的手挥过来——
    
        ***[（再来一拳！）]
        
        {hit_point_1>1:
        没人能理解你的感受——他们都比不上你此时的爽快！
        你还记得离家的日子，记得长途的跋涉与横眉冷眼，记得居无定所的疲惫与旷日持久的绝望——而这位贪婪的铁公鸡竟自顾自地站到你面前，伸脸让给你打！
        他的脸就在这里！
        
        -else:
        这是近乎自卫的举动——你必须抢得先手！
        }
        
        [体质检定] #speaker: #check:STR+1d8>challenge

        {CHECK:
        - SUCCESS:你再猛地抽出一拳，正中他的面部。他的表情顷刻间扭曲了，冲你来的手掌也变得软绵绵的。 
        ~hit_point_1++
        - HUGE_SUCCESS:你再猛地抽出一拳，正中他的面部。他的表情顷刻间扭曲了，冲你来的手掌也变得软绵绵的。他大喘着气，显然被刚才那拳给打得头脑发昏。
        ~hit_point_1++
        ~hit_point_1++
        - else:拳头划过他的脸颊。他抢先一步，将拳头糊在你的脸上。你感到一阵剧痛。
        
        [生命值减少5] #HP:-5
        
        }
        
        {hit_point_1 == 0 :
        连着两下失利，你一下子趴到地上。
        领队和那群偷渡者霎时间发出一阵大笑——显然是在嘲笑你的不自量力，随后便继续向前走了，并没有将你放在心上。
        你努力爬将起来。你身上已经有了一层泥。一股异常的愤怒与不甘涌上来，你几乎要流出眼泪。
        待那群人走过了，你才重新迈开脚步。
        ->solo_solo
        }
        
        {hit_point_1 == 1 || hit_point_1 == 2:
        忽然，领队做了个“停止”的手势。
        你见他摇摇晃晃的站着，脑袋并不清醒。
        再打下去……对我们都没好处。#speaker:领队
        我知道你厉害。你入队，我只收你到目的地的费用，好吗？
        }
        
        {hit_point_1 > 2:
        他忽然双腿一软，跪坐到地上。周围的人群发出一阵惊呼。
        他伸出一只手。
        求求你……别打了。#speaker:领队
        他用哀求的声音——伴着生涩的中文说着。你瞧见他的脸已经肿起来了，一大块紫色浮在面上。#speaker:
        我错了，我错了……我帮你把到目的地的费用……补了，好吗？你先把钱……拿着！#speaker:领队
        [金钱减少10]#object:-金钱*10 #speaker:领队
        }
        
        
            ****{hit_point_1 > 1}[成交。]
            
            见你答应了条件，他终于松了口气，将你安排到队伍中段。你一路走进去，周围的人群竟自发为你让道，显然都被你方才的举止给吓着了。
            你也说不清这到底是好事还是坏事。->solo_team
            
            ****[（离开）]
            
            你没有看他的脸色，而是径直将整支队伍都抛在脑后。
            你能够想象所有人惊讶的神色。无论如何，这出闹剧都没有改变你单独前行的意愿。
            当你回过神来，周边已经没有人了。
            
                *****[继续]
                ->solo_solo
        
        
        //***[（躲过去！）]  再写就太复杂了，以后再补足。
        ***[（趁机离开）]
    
        在事情闹得更大以前，你迅速闪过那个拳头，朝森林的深处奔去。
        脑后传来一阵阵的呼喊，你听不清他们在说些什么。
        当你回过神来，周边已经没有人了。
        
            ****[继续]
            ->solo_solo
    
    
    **[（绕道走开）]
    
    你懒得和他计较，顺势背身走开，远离了对方的骂声和那些疲惫的目光。
    当你回过神来，周边已经没有人了。
    ***[继续]->solo_solo
    

*[（补钱入队）]

在短暂思考后，你决定补钱入队。他向你开了价。

[智力检定] #speaker: #check:INT+1d8>trivial

 {CHECK:
 - SUCCESS:只有傻子才会接受这个价格——它几乎是你能接受范围的两倍，这是明抢！领队显然是想趁机宰你一顿。
 - HUGE_SUCCESS:只有傻子才会接受这个价格——它几乎是你能接受范围的两倍，这是明抢！领队显然是想趁机宰你一顿。
 - else:非常昂贵——但对于*补票上车*的人来说，它或许是值得的。
 }

    **[成交。（失去30金钱）]
    
    [金钱减少30] #object:-金钱*30
    
    看着你成交，旁人对你投来惊讶的目光，连那名领队似乎都因这交易而对你改观。你转过身，在称奇的感叹中汇入那一片队伍里。
    里面多是当地——或者更南方的居民。比起疑惑，他们更多投来羡慕的目光。
    无论这笔钱是否值当。你想到。它确实阴差阳错地填补了你虚荣的一部分。
    
    [心情增加20]#Mood:+20
    ->solo_team
    
    **[（给他一拳）]->hit_point
    
    **[（离开）]
    
    你没有接受条件，转身远离了队伍。就在你走出几步后，你听见有人喊了一句“穷光蛋”。你不禁咬紧了牙根。
        ***[继续]->solo_solo

*[（等待队伍经过）]

    你转身远离了队伍，等待他们走远。就在他们离开后，你听见雨林里有人喊了一句“穷光蛋”。你不禁咬紧了牙根。
        **[继续]->solo_solo

    -(solo_team)
    无论如何，你都加入了这片队伍，缓慢而坚实地走向雨林深处。->continue_1
    
    -(solo_solo)
    你继续独行在雨林中。
    天色渐晚，你也逐渐感受到这样的好处：不必开辟空间，何处都能是你的扎营地点。
    你拿出睡袋，将它裹在自己身上。你想象中的温暖并没有到来。夜色逐渐变得深邃，一股寒意浸入你的骨髓……
    
    [感知检定] #speaker: #check:WIS+1d8>trivial

    {CHECK:
    - SUCCESS:你听见一阵脚步声。
    - HUGE_SUCCESS:你听见一阵脚步声。
    - else:你听见一阵风声。
    }
    
    那声音被一阵急促的说话声打断。对方先是惊恐，随后竟带了些笑意。
    你在睡袋中打着冷颤。
    
    *[（离开睡袋）]
    *[（不作动弹）]
    *[（大喊）谁？]
    
    -你刚作出决定，一个人影迅速来到你的面前。你依稀能通过影子判断出他拿着的东西——一杆枪。
    它砸上你的脑门——周围变得更加漆黑。
    -后来的事刻骨铭心。
    你被当地的武装团伙挟持，经历了长达三个月的囚禁，最终在熟人的筹款下被交换出去。警方将你作为重点证人进行取证——实则是做做样子，心不在焉。连他们也对当地黑帮无可奈何。
    最终，在漂泊将近两年后，你被遣返回了那个熟悉的地方。
    再也没有机会了。这趟旅途持续得如此之久，又结束得如此之快，连更多感悟都没来得及出现——你甚至说不上这是欣慰还是幻灭。
    只是那股淡淡的遗憾永远萦绕心头。
    永远遥望，永远窃喜，永远悔恨，唯有一死得以解脱。
    
    [游戏结束] #HP:-100 #SAN:-100
    
    ->END
    
-(gang)

整备结束后，你循着莘先生指的方向找去，瞧见了好几簇零零散散的偷渡者。
你与几名蛇头交谈，大概确定了价格——并不便宜，但也无可奈何。前方是当地黑帮的地盘。

[智力检定] #speaker: #check:INT+1d8>challenge

 {CHECK:
 - SUCCESS:队伍越来越少。对剩下的生意人而言，多找到一名偷渡者都是意外横财。你凭借这一点——还有翻译软件——成功说服了一名蛇头以低价带队。
 你支付了定金，剩余的金额则在路程结束后补上。
 [金钱减少10]#object:-金钱*10
 - HUGE_SUCCESS:队伍越来越少。对剩下的生意人而言，多找到一名偷渡者都是意外横财。你凭借这一点——还有翻译软件——成功说服了一名蛇头以低价带队。
 [金钱减少10]#object:-金钱*10
 - else:队伍越来越少，情急之下，你只得加入一列价格高昂的团队。
 你支付了定金，剩余的金额则在路程结束后补上。
 [金钱减少15]#object:-金钱*15
}

在嘈杂人声的包围下，你迈开步，随整个大部队走入那一片郁郁葱葱。->continue_1

-(team)

整备结束后，你循着莘先生指的方向找去，瞧见了几名正焦急等待的偷渡者。
一块礁石立在他们中间，上面站了名当地人，正挥舞着手里的三角旗帜——比起蛇头，更像外地来的旅客。
你看过他给的价格——平价，甚至比你之前打听到的更加低廉。
在短暂的议价后，你在人群中找见了莘先生。在他的协调下，蛇头同意你先支付少量的定金，在路程结束后再支付剩余的金钱。
[金钱减少5]#object:-金钱*5
莘先生与你寒暄几句，随后，整个大部队都走入那一片郁郁葱葱。->continue_1

-(continue_1)
*[继续]-> END