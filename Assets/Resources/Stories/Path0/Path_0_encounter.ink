INCLUDE ../0-global.ink

->encounter

=== encounter ===

队伍停将下来。所有人都站在、蹲在原地，打算休息一阵。#speaker:
正在这时，你听见队伍前方传来一阵喧闹，似乎好几个人同时在大喊大叫。

[感知检定] #speaker: #check:WIS+1d8>trivial

    {CHECK:
    - SUCCESS:你听出来那是中文。
    - HUGE_SUCCESS:你听出来那是中文。
    - else:他们七嘴八舌，你听不太清内容。
    }

你走上前去，见两名黄皮肤的人正在和蛇头争执，身上沾满了泥土，衣服也破破烂烂的。

[感知检定] #speaker: #check:WIS+1d8>medium

    {CHECK:
    - SUCCESS:他们乍看起来像一对夫妻，女性的年龄却大上许多。
    - HUGE_SUCCESS:他们乍看起来像一对夫妻，女性的年纪却大上许多。
    - else:他们的穿着都很平常，只看得出来像一对偷渡者夫妻。
    }

忽然，那名中年女子瞧见了你，急促拍着同伴的后背。两对目光同时转移到你身上。
他们欣喜地冲上来，顷刻间便压到了你的身前。当然，这是见到“同胞”的反应。

妈呀！这群人讲不通道理！幸好遇到你了！#speaker:女偷渡者
你会中文，对吧？你是和我们是一样吧？#speaker:男偷渡者
他们用渴求的眼光盯着你。#speaker:

-(choice_1)

*[是的，我会。]->Chinese
*[滚开！]

你大吼一声，但他们并没有被吓到，反而靠得更近了。#speaker:
求求你了，我们需要帮助！#speaker:女偷渡者
先听一听，可以吗？#speaker:男偷渡者
他们轮流地快速说着，已经令你有些烦躁。

    **[说吧。]->Chinese
    **[（给他们两巴掌）]->punch


*[（假装自己是外国人）]->lie


-(lie)

长年累月的交流经验告诉你，要是想置身事外，“装蒜”永远是最管用的。
你看着他们的眼神。这是检验你的外语能力——或是其他能力的时候了。

*[（说英语）]->English
*[（说西班牙语）]->Spanish
*[（编造一门语言）]->New_language



-(English)

[智力检定] #speaker: #check:INT+1d8>medium

    {CHECK:
    - SUCCESS:你说了几句流利的口语。两人明显是听懂了——也没有对你的口音产生怀疑。他们很快就对你失去了兴趣，继续寻找人群里的“同胞”。
    还有什么比这更算得上对你语言能力的赞美呢？紧张过后，你只感到一阵欣喜。
    
    [心情增加10] #Mood:+10
    ->nothing
    
    - HUGE_SUCCESS:你说了几句流利的口语。两人明显是听懂了——也没有对你的口音产生怀疑。他们很快就对你失去了兴趣，继续寻找人群里的“同胞”。
    还有什么比这更算得上对你语言能力的赞美呢？紧张过后，你只感到一阵欣喜。
    
    [心情增加20] #Mood:+20
    ->nothing
    
    - else:你尝试说几句流利的英文，然而刚开口，两人就笃定这并非你的母语——甚至精确地知道了你的口音。
    
    你会说中文！#speaker:女偷渡者
    ->choice_1
    }
    
-(Spanish)

[智力检定] #speaker: #check:INT+1d8>legendary

    {CHECK:
    - SUCCESS:你说了几句流利的西班牙口语。两人皱了眉头，显然没有听懂内容，也没有对口音产生丝毫怀疑。他们很快就对你失去了兴趣，继续寻找人群里的“同胞”。
    还有什么比这更算得上对你语言能力的赞美呢？紧张过后，你只感到一阵欣喜。
    
    [心情增加20] #Mood:+20
    ->nothing
    
    - HUGE_SUCCESS:你说了几句流利的西班牙口语——几乎无可挑剔。两人皱了眉头，显然没有听懂内容，却也没有对口音产生丝毫怀疑。他们很快就对你失去了兴趣，继续寻找人群里的“同胞”。
    还有什么比这更算得上对你语言能力的赞美呢？紧张过后，你只感到一阵欣喜。
    
    [心情增加30] #Mood:+30
    ->nothing
    
    - else:你尝试说几句西班牙语，然而刚开口就变得磕磕巴巴，口音已经完全变成了中文的咬字。两人目瞪口呆地盯着你——当然，也知道了你的真实身份。
    你会说中文！#speaker:女偷渡者
    ->choice_1
    }
    
-(New_language)

当你想出这个念头时，你开始怀疑自己是不是疯了。
不，不，你是认真的。你相信这会是一场堪称“伟大”的欺诈——前提是你能成功。

[灵巧检定] #speaker: #check:DEX+1d8>challenge

    {CHECK:
    - SUCCESS:你很难相信自己是如何做到的。你胡诌出几个音节，随后大量地添油加醋，尽可能地组成一个词、一个句。在场的所有人都盯着你。半晌，两人确信你并不会他们的语言，最后皱紧了眉，继续寻找人群里的“同胞”。
    你松了口气——这件事或许值得说道一辈子。
    
    [心情增加15] #Mood:+15
    ->nothing
    
    - HUGE_SUCCESS:你很难相信自己是如何做到的。你胡诌出几个音节，随后大量地添油加醋，尽可能地组成一个词、一个句。转瞬之间，你竟然觉得自己真的掌握了这样一门生造的语言——里面有着合理的前缀和后缀，悠久的发音习惯和词汇演变。
    没有多少人关注你——你说话是如此自然，以至于他们真的把这当做某种未曾听说的语言。
    你为什么还要偷渡呢？你质问自己。你应该去当一名小说家——甚至语言学家。
    半晌，两人头也不回地走开了。而你已经彻底沉溺在“发明”新语言的欣喜之中。
    这件事或许值得说道一辈子。
    
    [心情增加至满值] #Mood:=100
    ->nothing
    
    - else:显然，你高估了自己的能力。你刚开口几句，那些生造的发音与字词开始变得磕磕巴巴，甚至中途停下来了一阵。
    在你反应过来时，两人已经站得更近。
    你会说中文！#speaker:女偷渡者
    ->choice_1
    }


-(Chinese)

谢天谢地！#speaker:女偷渡者
她用手腕用力抹了下太阳穴，表情抽动了一下，仿佛就要哭出声来。#speaker:
我们被当地人抢劫，东西都丢完了！#speaker:男偷渡者
那群人拿着枪，我这辈子第一次见那玩意。#speaker:女偷渡者
都不说枪——七八个人把我围着，我以为自己要交代在那儿……他们把我们的衣服扒光，从上到下检查一遍，甚至用剪刀剪我们的衣服……#speaker:男偷渡者
他们都是男的，我还以为自己要被强奸……#speaker:女偷渡者
两人说得越来越慢，声音也越来越小。#speaker:
幸好我们没有反抗，老实地把所有东西丢出去……#speaker:男偷渡者

[智力检定] #speaker: #check:INT+1d8>challenge

    {CHECK:
    - SUCCESS:你立刻发现了问题所在：如果他们所言属实，领队选择的这条线路并不安全——或者完全相反。
    看来有必要把蛇头给卷进来。
    ~know_the_cheat_1 = true
    - HUGE_SUCCESS:你立刻发现了问题所在：如果他们所言属实，领队选择的这条线路并不安全——或者完全相反。
    看来有必要把蛇头给卷进来。
    ~know_the_cheat_1 = true
    - else:两人都憋着眼泪。他们歇斯底里地说着，把被劫掠的怨气都发泄出来。
    }

我们才走到道中，只能一路向回求助，结果遇上了你们。#speaker:男偷渡者
我们也不求捎带一程——但我们想要点物资，先回去整备一番.#speaker:男偷渡者
还有……我们还得充电，然后联系家里面的人！#speaker:男偷渡者
他说得有些不好意思，甚至微微颔首。#speaker:
但这些服务是要收费的，所以……我们还要一点钱……#speaker:男偷渡者
我们保证还。你要是不放心，可以加联系方式，我们一定会还给你！#speaker:女偷渡者
他们用诚恳的眼神看着你。#speaker:

-(choice_2)

*[我可以给你们钱。（金钱-10）]

在商量结束后，你给了他们那些钱，换一份彼此的联系方式。你感觉他们快落了泪。
两人一边朝反向走去，一边朝你道谢。旁人大概知道怎么回事，向你投来钦佩的目光。

[心情增加20] #Mood:+20

->been_cheated


*[我和蛇头谈谈，帮你们加入队伍。]

等下！#speaker:女偷渡者
那名女偷渡者大叫出声，又立刻捂了嘴。#speaker:

{CHECK == SUCCESS || CHECK == HUGE_SUCCESS:
这显然是进一步证明了你的想法。或许他们根本就不是偷渡者——而是更令人忌惮的家伙。
-else:
你被她的声音吓了一跳。
}

[感知检定] #speaker: #check:WIS+1d8>easy

    {CHECK:
    - SUCCESS:那个男人猛拍女人的肩膀，随后站到前面来——这是个袒护的动作。他礼貌地给你道个歉。
    - HUGE_SUCCESS:那个男人猛拍女人的肩膀，随后站到前面来——这是个袒护的动作。他礼貌地给你道个歉。
    - else:那个男人站上前来，礼貌地给你道个歉。
    }

其实我们还有个旅伴。我们本来约好在下个村子碰头，结果半道就出了事。我们必须先回去等他。#speaker:男偷渡者
他的表情非常疲惫，眼睑半合着。#speaker:
我们没法一起走。#speaker:男偷渡者

    **[那怎么办？]
    
    我们已经身无分文了……要是能给我们一点钱，我们会非常感激。#speaker:男偷渡者
    我也恳求你——我们保证会还。#speaker:女偷渡者
    ->choice_2
    
    
    **[蛇头是专业的，我想问问他的意见。]
    
    你瞧见两人神色仓皇，仍然招呼了蛇头。#speaker:
    待他走过来的时候，两人居然已经不见影子了，留下你与领队面面相觑。
    
    {know_the_cheat_1 == true:
    想到自己把那两名骗子玩得团团转，你不禁窃喜起来。
    
    [心情增加20] #Mood:+20
    
    }
    ->not_been_cheated
    
    
    //还可以威胁，不过这里就不做了。


*(angry)[我没办法帮忙。]

不……求求你了，我们已经只能靠你了！#speaker:女偷渡者
她的声音近乎哀嚎。#speaker:

->choice_2

*{angry}[（给他们两巴掌）]->punch


-(punch)

他们已经越界了——你知道必须拿出威胁。
对待得寸进尺的家伙，掌掴往往比拳头更有效。

[体质检定] #speaker: #check:STR+1d8>easy

    {CHECK:
    - SUCCESS:你一巴掌糊在了那个女人的脸上，发出一记清脆的响声。在男人没有反应过来以前，你也给了他一巴掌。->punch_1
    所有人用不可思议的眼神看着你——那两人的眼睛里则带了些疑惑，最后转变成恐惧。
    - HUGE_SUCCESS:你一巴掌糊在了那个女人的脸上，发出一记清脆的响声。在男人没有反应过来以前，你也给了他一巴掌。->punch_1
    所有人用不可思议的眼神看着你——那两人的眼睛里则带了些疑惑，最后转变成恐惧。
    - else:在你挥出那一巴掌的瞬间，你就知道——坏事了。
    你没有控制好力道，那巴掌重重地打在那个女人脸上，力道却不输拳头。你敢打赌，你这辈子都没有打出比这更漂亮的一掌。响声远远回荡在森林里，四周一下子安静了。
    她径直倒在地上，无助地呻吟起来。->punch_2
    }
    
-(punch_1)

两人显然被你的举动吓到了。他们还想说什么，你抬起手掌，他们竟拔腿就跑，甚至没敢再回一次头。
你拍了拍手，蛇头向你投来畏惧的目光。
一股奇妙的爽快感油然而生。
[心情增加10] #Mood:+10
->nothing

-(punch_2)

你都干了什么！#speaker:男偷渡者
男人向你大吼，周围的人群也循着靠过来。所有人向你投来鄙夷的目光。#speaker:
你这样，你这样……#speaker:男偷渡者
他蹲下来，开始安抚地上的那名女性。那名女性的瞳孔已经无神了，只是痴呆地望着你——这一幕令人感到恐惧。#speaker:
蛇头已经站到了你的旁边。他皱紧了眉。#speaker:
给他们点钱吧，就当无事发生。#speaker:领队
这是在给你找台阶下。在你面前，男人竟大哭起来，那名女性仍然神情恍惚。你开始后怕她是否会因此落下病根。#speaker:
你身后的人群传来一阵哄闹，纷纷议论此事。事已至此，或许只有赎金的路子可走。

[心情减少20] #Mood:-20
[金钱减少20] #object:-金钱*20

->fail_punch


-(nothing)

待人群重新开始前行时，你仔细打量了一下这支队伍：里面果然不见两人的身影。
连你也是孑然一身，不可能照料路上的所有人、应对所有事。你对自己说。该把他们的事情抛在脑后了。
队伍走向森林深处，将海岸至此的过往都抛之脑后。
->continue


-(success_punch)

待人群重新开始前行时，你仔细打量了一下这支队伍：里面果然不见两人的身影。
你仍回忆着掌掴的手感……还有两人落荒而逃的样貌。
那两个人会如何呢？你或许永远没机会知道了。队伍走向森林深处，将海岸至此的过往都抛之脑后。

->continue

-(fail_punch)

待人群重新开始前行时，你仔细打量了一下这支队伍：里面果然不见两人的身影。
你仍回忆着掌掴的手感……你做了错事。
那两个人会如何呢？你或许永远没机会知道；又或许，你该侥幸自己没机会知道。队伍走向森林深处，将海岸至此的过往都抛之脑后。

->continue



-(not_been_cheated)

待人群重新开始前行时，你仔细打量了一下这支队伍：里面果然不见两人的身影。
你仍回忆着两人神色慌张的画面。
那两个人会如何呢？你或许永远没机会知道了。队伍走向森林深处，将海岸至此的过往都抛之脑后。

->continue

-(been_cheated)

待人群重新开始前行时，你仔细打量了一下这支队伍：里面果然不见两人的身影。
你沉浸在帮助他人的快感中，那名领队却忽然靠了上来。

我得坦诚说。#speaker:领队

[感知检定] #speaker: #check:WIS+1d8>medium

    {CHECK:
    - SUCCESS:他看似说得不情不愿——实际带了些试探，乃至于嘲讽。你感觉大事不妙。
    - HUGE_SUCCESS:他看似说得不情不愿——实际带了些试探，乃至于嘲讽。你感觉大事不妙。
    - else:他似乎说得有些不情不愿。
    }
    
*[怎么了？]

-我见过那两人，好几次。他们到处徘徊，专门骗钱。#speaker:领队
霎时间，你想起那两人的样貌，感到一阵胆寒。#speaker:

*[意思是我被骗了？]
领队点点头。#speaker:
不用沮丧，他们那样，很难看出来。#speaker:领队

*[你为什么不提醒我？]
我不敢提醒。他们似乎背靠当地势力。#speaker:领队
他顿了一下。#speaker:
还可能，黑帮有他们的人质，他们出来骗钱，为了赎人。#speaker:领队

-你呆在原地。无论两人作何动机，他们显然戏耍了你的良心。你的心情一落千丈。

[心情减少40] #Mood:-40

队伍走向森林深处，你仿佛还能听到那两人的声音——挥之不去，久久萦绕。
->continue

-(continue)
*[继续]
->END




