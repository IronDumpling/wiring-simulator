->beginning

VAR HP = 1
VAR SAN = 2
VAR HUN = 5
VAR MON = 0

=== beginning ===

-随一阵颠簸，你周遭的声音忽然大了。你听见橡胶与石头摩擦的声音，前方传来一阵惊呼……
- 你在哪？你感受着自己的身体……它瘫软着，倚在某个摇摇晃晃的事物上。在你旁边，某种黏腻的液体正反复碰撞，发出“啵”样的声响。

// select_1

 * 很陌生。->select_1_1
 * 是了——熟悉的海浪。->select_1_2

=select_1_1

你还记得潮湿的触感，那是淅淅沥沥的雨水，清爽、流动。可你感受到了——几滴液体飞溅到你的脸上，它们是沉重的，仿佛要将你的面部拖曳下去……
鼻腔向你发出警告。那是股咸腥的气味。
~SAN--
->going_1

=select_1_2

你还记得海风的触感，潮湿、咸腥。在岸上走动时，那股气息总是刺激你的鼻腔，直至熟稔。
可一股独属于泥土的腥气混进来——它越来越近了。
你回忆中的沿岸逐渐模糊，直至变了颜色……成为一片红色泥泞，还有生长其上的茂密雨林。
->going_1

=going_1

——你在哪？你再次诘问自己。
*尝试睁开眼睛。
-你睁开——又忽地眯了眼。
喑蓝色的深空中，太阳几乎缩成一个斑点，却比以往更加煞人。
你感受到你的皮肤，它先被汗给浸湿，又被晒干了水分，只留下一片油渍，甚至析出了盐来。
你慢慢起身。
*环顾四周。
-这是艘蓝色的橡皮艇。船上有十来个人，大多是黑色皮肤。
你就坐在他们中间，听那些人用陌生的语言谈笑。白色的浪花在你身旁涌动，说话声与波涛此起彼伏，又被更为嘈杂的嗡鸣所遮掩——那是发动机的噪音。
忽然，你瞧见了旁边的中年男人。他有着和你一样的黄色皮肤，戴一顶卡其色的鸭舌帽。显然，他和你来自同一个国家。他抱着背包，两脚叉得很开，做着非常悠闲的姿势。
似乎是注意到你的目光，他轻轻挑了下眉。

VAR NPC1 = "偷渡客"
VAR know_about_OceanSleepTime = false
VAR know_about_NPC1comeform = false

->dialogue_1

=dialogue_1

-{NPC1}- {怎么了？|还有问题吗？}
~temp question = 0
{know_about_OceanSleepTime:
~question ++
}
{know_about_NPC1comeform:
~question ++
}


//select_2
{

-question != 2: 

 *我睡了多久？
 ~know_about_OceanSleepTime = true
 ->select_2_1
 
 *你是从哪来的？
 ~know_about_NPC1comeform = true
 ->select_2_2
 
 -else:
 
 *我们好像快到了。
 ->going_2
 }
 
=select_2_1
 {NPC1}- 四个小时了，兄弟。
 {NPC1}- 哈，你心可真够大的。你至少该跟我打声招呼。
  *为什么？
 话音刚落，他朝你挪了挪身子，一边谨慎地环顾四周。
-{NPC1}- 没看到周围吗？他们全都是南美*黑鬼*。这群人手脚脏得很。
{NPC1}- 你想想，这群人要去国外避难，除了讨口子，多半是做过脏活。
{NPC1}- 说难听点，有些就是尾随你来的，专门盯着你动手。
 
 *你不能这么揣测别人。//select_2_1_1
 
 --男人笑出声。他在阳光里眯了眼。
{NPC1}- 哈，兄弟，这只是*自由之路*半途，咱们还没到地方。前面还有好几个小国，连着一整个墨西哥。
{NPC1}- 别对他们抱太大同理心。我给你举个例子。
说着，男人小心地指了指前头。你循着看过去，见那里正并排坐着三个高大的男人，咧着嘴谈笑。
{NPC1}- 左边那个杀过人，中间那个是强奸犯。
 
 **你怎么知道？
 {NPC1}- 他们刚才就在谈这事，甚至炫耀。
 
 ***你听得懂他们说话？
 {NPC1}- 来之前做了点功课，但是没用。给点钱就能请个翻译。
 {NPC1}- 总之，这趟路比你想得凶险。

->dialogue_1_going_1


 *感谢提醒，我会注意。//select_2_1_2
 
 {NPC1}- 嗯哼。你都到这儿了，最好别功亏一篑。
 {NPC1}- 我听说不少死半道上的，要么被偷了，要么被骗到没钱。

->dialogue_1_going_1

 
 =dialogue_1_going_1
 
 -{NPC1}- 而且——你已经被偷过一次东西了。

*什么情况？！

-{NPC1}- 就在你睡觉的时候，后面那个小个子想捞你的手机。
-{NPC1}- 别急——他没偷着，我正好瞧见了，他没敢动手。
-他说着，几乎是戏谑样地朝小偷笑笑。矮个子撇了撇嘴，咕哝出一句你听不懂的语言。男人笑得更大声了。
-{NPC1}- 瞧，别在这儿睡着了，不然就等着徒步穿行半个大洲吧。
-他安静下来，又恢复了先前的姿势。

 ->dialogue_1
 
 =select_2_2
 {NPC1}- 我？中国人呗。还能是新加坡的不成？
 ->selector_2_2
 
 
 =selector_2_2
 {NPC1}- {怎么，想和老乡聊聊天了？|接着问吧。}
 他大方地笑起来，似乎是来了兴致。
 VAR know_about_NPC1way = false
 VAR know_about_NPC1name = false
 
 {-not know_about_NPC1name||not know_about_NPC1way:
 
//select_2_2
*你叫什么名字？
{NPC1}- 我姓莘，草字头，底下一个辛苦的辛。
~ NPC1 = "莘先生"
~know_about_NPC1name = true

->selector_2_2

*你走的哪条线？
{NPC1}- 从土耳其到厄尔多瓜。沿途都有产业链，比想象中还方便。
{NPC1}- 前面走河道和陆路，到集结点以后，最后坐个车就能抵达哥斯达黎加。
{NPC1}- 这是最常用的路线了，稳妥一点。
~know_about_NPC1way = true

->selector_2_2

-else:

*你为什么会走线呢？
男人轻哼了一口气。
{NPC1}- 受不了体制呗。经济和政策烂成一团，稍微说两句都不行，笑死人了。更何况……
他忽然噤声了。似乎是意识到自己戾气太重，他咳嗽两声，又恢复到先前的姿势，敛了些笑。
{NPC1}- 大概是这样。听说不少走线的和我一样，但更多是生活所迫。
{NPC1}- 那你呢？
->select_2_2_3
}

=select_2_2_3

*国内生活过不下去。
{NPC1}- 哦，那很正常。大环境如此，上升空间也就那样了。
他自顾自地摸了摸下巴。
{NPC1}- 狗急了还会跳墙。人要是被逼着走线，那估计过得是真够惨的。
{NPC1}- 就当我同情你吧，老兄。
~HP = HP + 9
~SAN = SAN + 3
~MON = MON + 5

*我需要言论自由——哪怕一点。
{NPC1}- 那和我一样，理解。都替他们抗压了，连骂几句的资格都没有？
{NPC1}- 据说前面会遇到记者。要是你家里没啥挂念，就可以对着镜头，狠狠地骂一通。
{NPC1}- 要是成功了，还能激励更多人出来哩。
~HP = HP + 4
~SAN = SAN + 6
~MON = MON + 7


*我被国内封杀了，只能出来混。
男人显然是愣住了。半晌，他拍了拍你的肩。
{NPC1}- 不容易啊，兄弟。一整个国家机器都拿来对付普通人，真是畜生。
{NPC1}- 那你更要到地方了。到时候摊牌，把证据发出来，那帮人估计急得跳脚，又拿你没办法。
{NPC1}- 我看好你。
~HP = HP + 6
~SAN = SAN + 6
~MON = MON + 5

-他朝你竖一个拇指。
~SAN ++
->dialogue_1


 
=going_2

 你瞧见远处的陆地逐渐清晰。莘先生也向着那边眺望。
 {NPC1}- 是要上岸了，又要忙活一阵……
 说着，他拉开背包外层，一只手伸进里边摸索。很快，男人手里多了两袋塑料封装的面包。他将其中一个递给你。
 {NPC1}- 过会儿有得忙活，现在就吃吧。
 
 *谢谢。

 -获得消耗品：面包

（使用消耗品的教程）

随又一阵橡皮艇与石头摩擦的声音响起，小船靠上了海岸。你感觉身下一阵颠簸。船夫吆喝着偷渡者们下船。你和莘先生都起过身，稳稳地拿上自己的背包。
浅滩并不热闹，你只能远远望见零散的人影朝岸上走去。或许是海路过于凶险，鲜有人优先选择这条线路。
但更前边——当地的居民已经准备好了。

{NPC1}- 我先去充会儿电，你可以再来找我，就当搭个伴。
{NPC1}- 对了，建议你找个翻译，按我的经验，行动要方便得多。蛇头那边应该就有不少。
{NPC1}- 就这样——祝你顺利。

 *也祝你顺利。
 *再见。

-->ShoreVillage

 
=== ShoreVillage ===

VAR NPC2 = "志愿者"
VAR NPC3 = "便利店老板"
VAR NPC4 = "蛇头"
VAR NPC5 = "翻译"


{TURNS_SINCE(->ShoreVillage) == 0:

和莘先生作别后，你站定在聚落中央。
一条宽敞的泥泞大道贯穿南北，各种设施稀稀散散地摆在路边。人群聚集在聚落东侧——蛇头正用各种各样的语言呼喊着客源。他们旁边是一座破烂的木屋，上面挂一张同样破烂的牌子，用中文歪歪扭扭地写了“便利店”三个大字。它对面呢是一座低矮的白色帐篷，已经被泥巴和灰尘涂得乌黄了，像是游牧民族的房包。
一阵大风从前方吹来，在聚落里扬起沙尘，直奔你身后的海洋。
你忽然觉得：这片聚落是如此空旷，又是如此拥挤。
——该决定先做什么了。

//节点一的初始参数

~temp ShoreVillageTurn_1 = 0
~ShoreVillageTurn_1 ++
VAR been_tent_1 = 0
VAR been_shop_1 = 0
VAR been_smuggler_1 = 0
VAR negative_event_1_complete = false
VAR negative_event_2_complete = false
VAR negative_event_3_complete = false
VAR negative_event_4_complete = false
VAR translator_you_meet = 0
VAR translator_you_employ = 0

-else:

一座小型聚落敞在面前。建筑大多是临时搭建的棚屋，显然跟随偷渡者的脚步而来。
要去哪呢？

~ShoreVillageTurn_1 ++

}


//select_3

+志愿者帐篷 
->position_tent_1
+便利店
->position_shop_1
+蛇头
->position_smuggler_1 

+{ShoreVillageTurn_1 > 2}休息站
->position_restroom_1


= position_tent_1

{been_tent_1 == 0:
待你走近了，你才看清那顶帐篷上的红色十字：它已经被泥巴糊成灰黄。按你的经验，这应当是无国界医生的帐篷。
你走进去，见前台正坐着一名白人女子。她瞥你一眼，给你指指桌上的一沓文件。
你跟着过去看看：那是份纸质表格，上面密密麻麻地写了两列。左边一栏混杂着各种语言，大多是拉丁文，还有些简洁的中文汉字，显然都是姓名；右边则是日期，即便国籍和语言差异巨大，这些阿拉伯数字仍然整齐划一地标注着今日。

~been_tent_1 ++
VAR know_doctor_1_can_speak_Chinese = false
VAR konw_you_can_buy_EOL = false
VAR buy_the_EOL = 0
VAR know_the_tent_post_1 = false

-else:

有些透风的帐篷里边，三个人安静地做着工作。
~been_tent_1 ++

}


*{not know_doctor_1_can_speak_Chinese}（向女人搭话）你会说中文吗？
->NPC_doctor_1

+{know_doctor_1_can_speak_Chinese}（向女人搭话）我想问问。
->NPC_doctor_1

+{konw_you_can_buy_EOL}{not negative_event_4_complete}朝两名工作人员打手势。

他们会过意，假意到帐篷外面与你谈笑——实际上是用药物和食品换了你的一张钞票。（金钱-1）
~MON --
~buy_the_EOL ++

->position_tent_1

*填上个人信息

+离开
->ShoreVillage

-你麻利地填上信息，很快，旁边的两个黑人靠过来。他们冲你笑笑，给你递了一大一小的两个盒子。
你端详了一下：大的是一盒干粮，小的则是预防热带病的药物。
（获得援助干粮、临时药品）
收下东西，你正要走出去，却发觉那两个人正用奇怪的眼光盯着你，似乎祈求着什么。
{translator_you_employ > 0 :
 
  {NPC5}- 这应该是在讨钱。
  你的翻译提醒你。
 }



*给他们塞钱。（金钱-1）
~MON --
 你摸出一张皱巴巴的美元钞票。他们见状，果然都跟上来。
 你刻意避开那名前台的视线，在角落把钞票交给他们。两人对视一眼，又小心地塞了两个盒子给你。
 （获得援助干粮、临时药品）
 随即，他们小声地向你说了些什么，又指指物资。你听得不太清楚。
 
 
 
 {translator_you_employ == 0:
 他们又说了一遍。你确认自己没办法听懂，只能挥挥手，任凭他们回到岗位上。
 }

 {translator_you_employ == 1||translator_you_employ == 2 :
 你的翻译忽然也靠过来，几乎是凑到你耳边了。
 {NPC5}- 他们愿意用低价卖你这些物资，你可以再买。
 他的声音同两名工人一样低。似乎见你理解了意思，工作人员都兴奋地笑笑，随后又回到岗位上了。
 ~konw_you_can_buy_EOL = true
 }
 
 {translator_you_employ == 3:
 这名粗犷的翻译忽然眼神一亮。
 {NPC5}- 我认识他们！
 他高声说着，连前台的志愿者都抬起头。随即，他朝前几步，开始与那两人攀谈。
 他们说话的声音越来越小，都低着头，不时朝你投来一瞥。
 很快，你的翻译又走到你旁边。
 {NPC5}- 他们说做腻了。如果你想，可以做他们的岗位，能赚钱。
 ~know_the_tent_post_1 = true
 
 他顿了一下，似乎在犹豫什么，脸也红起来了。
 {NPC5}- 你还可以花钱，买这些物资。他们愿意卖。
 他的声音同两名工人一样低。似乎见你理解了意思，他们都兴奋地笑笑，随后又回到岗位上了。
 ~konw_you_can_buy_EOL = true
 }
 


*给他们竖个拇指。
他们看到你的举动，先是面面相觑——又摇了摇头。
你听见志愿者朝他们喊了什么，两人又转过身，投入了下一轮工作。

{translator_you_employ == 1:

  {NPC5}- 别在意。这种讨钱的到处都有，总不能都给好处。
}

{translator_you_employ == 2:

  {NPC5}- 说实话，有点出乎我的意料……
  他的笑容僵在脸上——他想笑，却不敢出声。

}

{translator_you_employ == 3:

  {NPC5}- 哎呀，希望他们别在意。
  他轻叹口气，显然是在惋惜——但不是对你，而是对里边的两个。
}

你尴尬于这场失败的交流。但至少……就当鼓励了无国界组织吧。
~SAN ++


*（喊话）嘿，兄弟，你们想要什么吗？
你刚喊出声，就听到旁侧的一阵响动。是那名志愿者。她站起身，打翻了身旁的一叠纸巾。
{NPC2}- 他们干了什么？

{not know_doctor_1_can_speak_Chinese:
你愣了一下。在场并没有其他人——只有你们四个。
{NPC2}- 先生？
你张大了嘴。错不了，那几句流利的中文是从志愿者口中说出来的：一个怎么看都是欧洲面孔的白人。
}

**没什么。
{NPC2}- 那最好。这里物资是溢出的，但总不能倒卖。
她瞥一眼旁边的工作人员——你看出来，比起厌恶，她眼神里更多的是悲悯。
{NPC2}- 祝你顺利吧，先生。
又有人走进来。志愿者没再看你，立刻便换了种奇怪的语言，开始与下一位难民交谈。


**他们像是在向我求助。
{NPC2}- 该死……
她转过头，用当地语言——发音极为狰狞，应当是什么粗口——朝那两人呼喊。黑人们没有回嘴，而是用堪称迁怒的眼光瞟了你一眼。你感觉很不舒服。
~SAN--
志愿者很快又换了中文，显然是向你发话。
{NPC2}- 小心点，他们经常倒卖库里的过期物资。在森林里用这些东西，可能要你的命。
她皱了眉，轻轻叹了口气。
帐篷里陷入一阵沉默。你意识到——该离开了。

-你走出帐篷。
->ShoreVillage

=NPC_doctor_1

{know_doctor_1_can_speak_Chinese == false:

{NPC2}- 我会。

~know_doctor_1_can_speak_Chinese = true

你发觉她疲惫地叹了口气。似乎是念在人少的份上，她才不得已向你回应。
{NPC2}- 有什么需要帮助的吗？

-else:
{NPC2}- 还要问什么吗？
}

+你是政府的志愿者？
{NPC2}- 不，我不是政府的人。那些人在集结点。
{NPC2}- 这里是私人设置的援助区域。我和那边两个当地人一样，都是雇来的。
她朝帐篷出口偏了偏头。你看到那边的两个黑人正在整理小山般的盒子。

++私人的？
{NPC2}- 没错，手术和治疗都要收费。你可以当成私人诊所。
{NPC2}- 不过我们和政府有合作关系。每个月都有人道主义援助送过来。你登记一下，就可以免费领取食物和药品。
她说得很小声，又指了指桌上的表单。

->NPC_doctor_1

+你知道我该怎么去集结点吗？
{NPC2}- 唉……
她重重地叹了口气，几乎是故意让你听到了。
{NPC2}- 穿过雨林，到巴霍奇基托，然后坐船到拉哈斯布兰卡斯集结点。
{NPC2}- 徒步，翻山，坐船，甚至坐飞机——随你怎么走。要么提前规划好，要么去问蛇头。求求你了，别来问我这种事，哪怕是拿我当消遣。
她明显已经很烦躁了。你能瞧见她浓重的黑眼圈。

->NPC_doctor_1

+没什么了。
{NPC2}- 那最好。

->position_tent_1


= position_shop_1

  {been_shop_1 == 0:
   刚走进门，你就瞧见了一个木头柜台——还有坐在后面的黄种人老板。柜台上放了两个大插排，上面密密麻麻地接了一堆电子设备。电器的主人们就站在门外，一边盯着，又一边打着呵欠。
   整个商铺人员稀少，或许是少有人选择在此处停留——毕竟过了雨林，就是可以直达边境的集结点。
   你准备挑选东西，一个熟悉的身影又闯进你的视野。是那个曾经想要偷你东西的矮个子。他在货架后盯着你。
   你们的目光对视一瞬，随后，他径直从你身旁经过，又咕哝一句你听不懂的话。
   
     ~been_shop_1 ++
     
     ->negative_event_1
   
   -else:
   空旷的货架——还有戴着墨镜的便利店老板。
   
  }
    
    +看看食品架。
    ->position_shop_1
    +看看药品架。
    ->position_shop_1
    +更换货币种类。
    ->position_shop_1
    +离开吧。
    你走出这栋破旧的木屋。等候设备充电的人们瞥了你一下，随即又垂下眼眸。你同他们一样疲惫。
    ->ShoreVillage

  = negative_event_1
    
    ~temp quick_time = false
    
    *{translator_you_employ > 0}（向翻译询问）他说什么？
    {NPC5}- 俚语，意思是“傻瓜”。
    
    {translator_you_employ == 1 || translator_you_employ == 2 :
    {NPC5}- 听起来像本地口音，应该不是走线的。这种人最危险。
    
    ~quick_time = true
    
    }
    
    {translator_you_employ == 3:
    {NPC5}- ……或者“笨蛋”。
    他若有若无地补上一句，似乎想纠正词语的意思。你感觉蠢透了。
    }
    
    **{not quick_time}有些不对……
    好一阵，你才想到检查自己的随身物品。你就地脱下背包，开始翻看里边的东西。很快，你感到一阵眩晕：一整套充电器和电池都不翼而飞。
     ~SAN--
    你冲出门，而那个矮个子已经消失在人群里。
    
      ***回到店里。
    
    
    **{quick_time}迅速检查自己的背包。
    你就地脱下背包，开始检查里边的东西。很快，你感到一阵眩晕：一整套充电器和电池都不翼而飞。
     ~SAN--
    你冲出门，见那矮个子正站在一名蛇头的边上。
      
      ***跟上去。
      ->negative_event_2
      
      ***回到店里。
    
   
    
    *{not quick_time}有些不对……
    好一阵，你才想到检查自己的随身物品。你就地脱下背包，开始翻看里边的东西。很快，你感到一阵眩晕：一整套充电器和电池都不翼而飞。
     ~SAN--
    你冲出门，而那个矮个子已经消失在人群里。
    
      ***回到店里。
   
    -{NPC3}- 招惹本地人了？ 
     老板忽然向你发话。他扶了扶自己的墨镜，你能感受到那张干巴面孔上的笑意。
     {NPC3}- 那你得注意点，指不定哪天就被介绍到某个黑蛇头那边，然后把你卖了。
     {NPC3}- 他们最喜欢的就是中国人，身价高，还好赎。
     
     **他偷了我的东西！
       {NPC3}- 那大概是拿不回来了。或者你还记得他的样子，可以去问问蛇头。
       {NPC3}- 不过对咱们来说，这群人都长得千篇一律的，都不知道怎么描述。
       他装模作样地叹了口气。
       {NPC3}- 难办哦。
     
     **（瞪着他）
       {NPC3}- 哦，哦！别急，老乡，我没看到作案过程。
       他的语气带着嘲讽。
       {NPC3}- 说不定早被偷了，只是现在才发现。别误会，我是同情你的。
     -- 
     **……你是中国人？
      {NPC3}- 当然。这儿的黄皮肤都是中国人。
      他向后躺到了椅子上。
     
     {translator_you_employ == 0:
       
       {NPC3}- 我建议你到一个新地方就先找翻译。蛇头那儿就能叫几个。
       他点了根烟，一边朝门外指指。
       {NPC3}- 不包安全，有的是马货，但至少遇到本地人不会什么都不懂。
       {NPC3}- 你也可以自己学。只要有闲心。
       显然，他在拿你打趣。
       
       }
      
       ***你能介绍安全路径吗？
       他朝你摆摆手。周围的烟气被他扇去了。
       {NPC3}- 别问我，我没走过线。
       {NPC3}- 你们都是有去无回，我哪知道死了多少？
       他仍是那副皮笑肉不笑的表情。你感觉脊骨发麻。
    
      {translator_you_employ == 2:
    
      你注意到，便利店老板似乎一直盯着你和你的翻译。但他一直保持着那张笑脸，很难判断用意。
    
      }
    
       {NPC3}-行了，你自己看吧。
       
       这基本是下了逐客令。你只得站到几排寒酸的塑料架子跟前。
       
       ~negative_event_1_complete = true //被动事件1结束
    
     ->position_shop_1



= position_smuggler_1 
    
    {been_smuggler_1 == 0:
    一群穿着旅行套的人集中在五颜六色的旗帜前。看着这幅景象，你想起旅游风景区的游客。有些蛇头揽不着人，像黑车司机一样东拉西扯。
    你看不出他们的区别——只能碰碰运气，或者想别的办法。
    
    
    ~been_smuggler_1 ++
    
    -else:
    
    你看着闹哄哄的人群。有时候，走线的成败在此一举。
    
    }
    
    *找团队大一点的蛇头。
    一个接近二十人的团队吸引了你的注意。队伍闹哄哄的，显然是推搡着蛇头赶快出发；但他还想再揽些客源。就在此时，他也瞧见了你。
    
    {NPC4}- 要加入吗？马上，就出发了。
    他磕磕绊绊地说着中文，带着浓烈的地方口音，仿佛有块粘牙的糖。有几名偷渡客似乎按捺不住，朝他吼了几声。他没有回头。
    {NPC4}- 赶快，快。
    
     **路程有多久？
        
    {NPC4}- sa天。
    他伸出三根手指。你知道他是说“三天”。
    
     ***多少钱？
     
    他给你比了个数。（金钱-3）
    {NPC4}- 安全。包安全。
    他继续吃力地比划着。
     
     {not negative_event_3_complete:
     你忽然想起那个戴鸭舌帽的男人——你还没和莘先生联系。
     但走线客的分别本就是常态。先走一步，或许也可以在之后碰头。
     }
     
     ****好，现在就走吧。
     {translator_you_employ > 0:
     
         你和翻译攀谈几句，便就此别过。
     
     ~translator_you_employ = 0
     
     }
     
     ->leave_ShoreVillage  
     
     ****我还没准备好。下一班是什么时候？
    {NPC4}- 都有，每天都有。
    似乎是意识到你不打算即刻加入，他很快转过身，挥了挥手里的旗子。人群跟着他朝道路尽头走去，聚落转瞬间变得空荡不少。
    你忽然明白，加入这种队伍无疑是安全的——但是会多花不少时间。
     ->position_smuggler_1
    
    +找人少的蛇头。
    
    {not negative_event_3_complete:
     你忽然想起那个戴鸭舌帽的男人——你还没和莘先生联系。
     但走线客的分别本就是常态。先走一步，或许也可以在之后碰头。
     }
     
     ++好，现在就走吧。
     {translator_you_employ > 0:
     
         你和翻译攀谈几句，便就此别过。
     
     ~translator_you_employ = 0
     
     }
     
     ->leave_ShoreVillage 
     
     ++我还没准备好。
     
    似乎是意识到你不打算即刻加入，他很快转过身，挥了挥手里的旗子。
     ->position_smuggler_1
    
    
    ->position_smuggler_1
    
    +雇佣一名翻译。
    ->employ_translator
    
    
    +离开。
    ->ShoreVillage
    

->ShoreVillage


  = employ_translator
  
  {TURNS_SINCE(-> employ_translator) == 0 :
  即使蛇头不懂中文，一般也会有翻译作为中介，并拿走部分抽成。这里并不缺翻译——不如说，他们总会自己找上门来。
  
  {translator_you_employ > 0:
  你原先的翻译看着你，耸了耸肩。
  }
  
  }
  
  
  
  {shuffle:
    -{translator_you_employ != 3:
    一个高大的当地人用英文向你问话。你和他谈了几句，大概能明白对方的意思。他的中文并不算好，但人很宽厚，开价也很便宜。你觉得他很适合当“观光客”的导游——而不是走线客的。
    
    ~translator_you_meet = 3
    
    -else:
    你没有找到。你原先的翻译接着推荐自己。
    
    ~translator_you_meet = 3
    
    }
    -{translator_you_employ != 2:
     一名精瘦的男子同时和几个人谈着话。似乎是生了气，他甩开几人，径直朝这边走来。他用极度流利的中文向你介绍自己：他是名翻译，也可以带蛇头的工作，不过要价更高。
     
    ~translator_you_meet = 2
    
     -else:
    你没有找到。你原先的翻译接着推荐自己。
    
    ~translator_you_meet = 2
    
    }
    -{translator_you_employ != 1:
     -一名黄种人朝你挥手。他也是一名走线客，不过因为金钱不够，被迫留在此处打工。为了验证自己的能力，他将你的话转述给当地人——非常准确，当然，价钱也很昂贵。
     
    ~translator_you_meet = 1
    
     -else:
    你没有找到。你原先的翻译接着推荐自己。
    
    ~translator_you_meet = 1
    
    }
  }
    
    
    +再找找。
    ->employ_translator

    +{translator_you_employ != translator_you_meet}雇佣他。
    
    {translator_you_employ > 0:
    你和原先的翻译作别。
    }
    
    {translator_you_employ == 0:
    翻译站到你的旁边。你不太习惯说话都要依靠别人的感觉——仿佛嘴巴不是自己的。
    
    }
    
    ~translator_you_employ = translator_you_meet
    
    
    +该打住了。
    
    -->position_smuggler_1
    
    


  = negative_event_2

->ShoreVillage



= position_restroom_1

与其说休息站——这里更像是一排带顶的木头椅子。偷渡客们或坐或躺在椅子上，两脚护着底下的大包小包。
不同肤色的孩子们在走道上奔跑、玩耍，他们还不知道自己从何处来——又将往何处去。
忽然，你瞧见了那顶熟悉的帽子。
->negative_event_3

 
*离开。

->ShoreVillage

= negative_event_3

 *又见面了，莘先生。
 他仍然保持着船上那个舒适的姿势：两腿分开，把背包夹在里头；身体则靠在椅子上。
 {translator_you_employ == 1:
 
 {NPC5}- 莘先生……
 翻译也嘀咕着他的名字。
 
 }
 
 
 {NPC1}- 哦，老兄，亏你能找到我。
 他忽地抬起头，朝你笑笑。
 
 

-> END

= leave_ShoreVillage  

-> END



   