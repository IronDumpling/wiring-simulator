INCLUDE ../0-global.ink

->beginning

=== beginning ===

你还记得此前偷渡的日子。你曾在城市与村落间辗转反侧，同警察和边防“斗智斗勇”，撒下弥天大谎。
那是一趟破破烂烂的旅程，连你的对手也破破烂烂的。那位警察甚至直言不讳地问道：“你有偷渡的想法吗？”
你面无表情地撒了慌，随后找到蛇头，将金钱与过往一同掷了出去。
你最终来到这里，第一次踏入南美的雨林。
山坡朝下，你和队伍走着并不能被称之为“路”的狭窄通道，正谨慎地下行。#speaker:

[灵巧检定]#check:DEX+1d8>medium
{CHECK:
    - SUCCESS:于你而言，路线不算太过崎岖。相较于地面，你更在意前方是否会遇到毒虫和劫匪。
    - HUGE_SUCCESS:所幸，这样的泥泞路子于你而言并不算崎岖。相较于地面，你更在意前方是否会遇到毒虫或是劫匪。你的步速是如此之快，几乎与领头齐平。他不时看你几眼。
    - FAIL:你走得并不平稳。你能感觉到鞋子在泥潭一样的下坡路段打滑，整个身体只得借助四周的树干，或是旁人的搭手。想到前方更可怕的路途，你打了个寒颤。
    [心情减少10] #Mood:-10
    - HUGE_FAIL:你走得并不平稳，几乎每行几步就会打滑——甚至一度摔入一片泥泞，整个队伍都因此停步等待一阵。疼痛之余，你感到羞愧难当。
    [血量减少5][心情减少20] #HP:-5 #Mood:-10
    }

这里是名副其实的热带丛林：树木遮天蔽日，潮湿与腐烂的气味涌入鼻腔。道路没有阳光滋养，也没有你熟悉的寻常矮灌，唯有青苔在岩壁与树干上厚厚生长，爬到及人高的位置，令你无从倚靠。
泥泞仿佛没有尽头，你的精神也在这片湿气与阴影之中恍惚。径流与峭壁有阳光洒下，可永远伴随更为凶险的自然环境。
总有偷渡者或走线客越过这片丛林。你想到。他们或与你一样借助当地人的力量，或只凭着远在天边的定位卫星在这片森林中摸索。
无论如何，你已经来到此处：阿坎迪雨林，凶险旅途的开端。

*{CHECK == HUGE_SUCCESS}[（与领队谈话）] ->talk_with_leader   
*[（前行）]

-(continue_1)
*[继续]
->END

= talk_with_leader

{在你旁边，领队经常回头观望是否有人掉队。他几乎不喘大气，熟练地用棍子拨开前路，仿佛只是在玩乐而已。很快，领队注意到你的眼神，示意样地点了下头。|他从鼻腔出了口气，继续盯着你。}

{你想问什么吗？|还想问什么？}#speaker:领队

-(talk_with_leader_1)

+[你见的中国人多吗？]

不多。大多是，更南边来的。你们不一样。#speaker:领队
他磕磕巴巴地说着中文，对你投来——尽力的——友好的目光。#speaker:

[智力检定]#check:INT+1d8>easy
{CHECK:
    - SUCCESS:这也难怪。走线客越来越多，当地人已经把中国人视作摇钱树。中文是业务的一环——也自然希望你能因此给出更多报酬。他尽力敛着焦急——却也急不可耐。
    - HUGE_SUCCESS:这也难怪。走线客越来越多，当地人已经把中国人视作摇钱树。中文是业务的一环——也自然希望你能因此给出更多报酬。他尽力敛着焦急——却也急不可耐。
    - FAIL:你看不出他究竟是耐心还是烦躁。
    - HUGE_FAIL:你看不出他究竟是耐心还是烦躁。
    }
    
到了地方才能，收钱。所以，不用担心。#speaker:领队

->talk_with_leader
    

*[你经常走这条路？]#speaker:

我带过二十几次队，没事故。#speaker:领队
这条路五六次，还不熟练。

    **[路线有变化？]#speaker:
    
    不一样。这里都是打劫。一样太久，就会被截。#speaker:领队
    
    [智力检定]#check:INT+1d8>formidable #speaker:
    {CHECK:
        - SUCCESS:是很有道理。不过雨林不是哪里都宜居，就算劫匪也会有据点。向导们或多或少有自己的情报渠道。
        - HUGE_SUCCESS:是很有道理。不过雨林不是哪里都宜居，就算劫匪也会有据点。向导们或多或少有自己的情报渠道。
        - FAIL:是很有道理。想到那群拿着枪的人，这段路忽然也变得和蔼可亲了。
        - HUGE_FAIL:是很有道理。想到那群拿着枪的人，这段路忽然也变得和蔼可亲了。
    }
    
    --(talk_with_leader_2_1)
    
        ***{CHECK == SUCCESS || CHECK == HUGE_SUCCESS}[土匪都在哪活动？]
        
        靠河。如果看到，有屋子在河边，就要远离。他们声音很大。#speaker:领队
        我们有段路会度过，我到时候说。
        显然，他是指有一段路很可能会遇上那群土匪。你最好提前做准备。#speaker:
        ~know_the_rob = true
        
        ->talk_with_leader_2_1
        
        ***[劫匪都是什么人？]
        
        有些是犯过罪的。他们最狠，把人关进去，然后等赎金，赎不起的卖给西边。那边有人收。#speaker:领队
        还有组织，一整个村。他们抢过，觉得好，就一直抢。但也不杀人。
        我们顶多，遇到后者。犯罪的人少，不抢团队。
        这是项单方面保证，但你还是松了口气。
        [心情增加10] #Mood:+10
        
        ->talk_with_leader_2_1
        
        +++[了解了，谢谢。]
        
        ->talk_with_leader
    


+[还有多久才到？]

这才开个头。还要两天两夜。#speaker:领队
我们要在林子里睡两晚，晚上很冷，我们要开个平地。最好搭个帐篷，用睡袋，靠着树睡也可以，塞点衣服，暖和得多。#speaker:领队
~know_the_sleep_skill_1 = true

->talk_with_leader

+[没什么。]

对方也没有多做声，只是继续往前走了。
队伍沉默地在树木间穿梭。

->continue_1