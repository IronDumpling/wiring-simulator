INCLUDE ../0-global.ink

->beginning

=== beginning ===

你趟过那片充满灰尘的简陋村庄，再度面向大海。在日光的照耀下，蓝色的海水显得亮莹莹的，同周遭灰色的一切格格不入。 #speaker:
海面的另一头是你的目的地：卡雷托海滩，一片礁石丛生的民间岸口——著名的海难地点。这片风平浪静的海域一到晚上便性情大变，狂风与波涛不绝。但为了顺利避过检查，简陋的船只只得借助黑夜的掩护，同风浪作伴，将船夫与偷渡者的性命完全交由风暴。
你丢弃多余的行李，登上了一艘快艇。快艇表面坑坑洼洼，满是风浪与暗礁击打的痕迹。它们在后几夜将新添不少。

[感知检定] #check:WIS+1d8>easy
{CHECK:
    - SUCCESS:你瞧见了船夫——还有他布满血痕的伤腿。那些伤口横纵交错，新旧叠加，光是看着便令人隐隐作痛。
    - HUGE_SUCCESS:你瞧见了船夫——还有他布满血痕的伤腿。那些伤口横纵交错，新旧叠加，光是看着便令人隐隐作痛。
    - else:你瞧见了船夫——他完全符合你对“硬汉”一词的想象，伤疤和帽子都横在他头上。
    }

快艇的规模不大，十来人挤坐在船只中央。你的位置在第二排——相当靠前的地方，远离了最热闹的中心部分。
海面吹来一阵和煦的微风。船夫在快艇头部走来走去，享受着阳光和风的滋养，似乎对未来的一切满不在乎。

*[（向他搭话）你好？]

    他转过头来，先舔了舔自己干裂的嘴唇。#speaker:
    --(talk)
    
    {你想问什么吗？|你还想问什么？}#speaker:船夫
    {他好奇地盯着你。你对他娴熟的中文感到惊讶——他显然已经接待过很多像你这样的人了。|他笑了笑，鼻壑照出深深的影子。|他又舔了舔嘴唇，等着你的下一个问题。|他在船头活动了下小腿。你能瞧见上面皱巴巴的伤口。|他挠了挠头，转头眺向海面。}#speaker:
    
    **(Chinese)[你干这行很久了吗？]
    
    哈，恰相反。我从三十多岁干到现在，只有短短十多年。#speaker:船夫
    只是我运气更好，年龄更大或者更小的容易进鱼肚子。
    他说话的腔调令你感到熟悉——你甚至怀疑他是来自你家乡的海员。#speaker:
    ->talk
    
    **{Chinese}[你的中文说得很熟练。]
    话音刚落，你瞧见他笑眯了眼。#speaker:船夫
    或许吧，我认识几个说中文的朋友。之前这里有个年龄很大的海员，就是在偷渡途中定居的。
    
        ***[定居？]#speaker:
        我也不知道他打的什么算盘，不过从某天起就没见过他了。#speaker:船夫
        他比我还老练，应该不是死在海难里。#speaker:船夫
        ->talk

    **(skill)[你很熟悉这条海路吗？]
    
    当然——这是沿岸最危险的一条道。只有傻子和偷渡者才会尝试在卡雷托登陆。#speaker:船夫
    他故意把“偷渡者”从“傻子”里剔出去，像是开了个不紧不慢的玩笑。#speaker:
    最危险的就是登陆那晚，几乎必定会遇上风暴。稍微有一点风或者浪打到船上，很大可能会完蛋。#speaker:船夫
    他说得很严肃——语气却很俏皮。#speaker:
    这是门技术活。别担心，要是我翻了船，我就不可能站在这儿。#speaker:船夫
    ->talk
    
    **{skill}[遇到危险有什么诀窍吗？]
    
    抓好你周遭的东西，别被甩下船。除非船翻了。#speaker:船夫
    要是落到水里，利用好救生衣——或者别的什么东西的浮力，听天由命。
    就这样，没别的了。
    ->talk
    
    **{Chinese || skill} [没什么了。]
    
    很好，我们也快出发了。#speaker:船夫
    
    ->continue
    
    


*[（默不作声）]

你不想浪费更多时间在对话上。最艰难的一段路就在前面——你已经做好准备。

-(continue)
船夫向所有人分发了混着泥浆的救生衣，几乎看不出橙色了。你知道，它更难被发现——而你若被甩下船去，也更难获得救援。
随着发动机发出轰鸣，船只驶离了灰色的海岸，朝着还未生成的风暴而去。

*[继续]
->END